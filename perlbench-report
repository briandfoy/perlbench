#!/usr/bin/perl -w

use strict;

BEGIN {
    unshift(@INC, "lib") if -f "lib/PerlBench.pm";
}

use PerlBench::Utils qw(sec_f);
use PerlBench::Stats qw(calc_stats);
use PerlBench::Results;
use Getopt::Long qw(GetOptions);

my $resdir = "perlbench-results";
my $merge;
my $filter_activeperl;
my $filter_gcc;
my @filter_version;
GetOptions(
   'result-dir=s' => \$resdir,
   'merge' => \$merge,
   'filter-activeperl' => \$filter_activeperl,
   'filter-gcc' => \$filter_gcc,
   'filter-version=s' => \@filter_version,
) || usage();

my $cmd = shift || usage();
$cmd =~ s/-/_/g;
$cmd = "cmd_$cmd";
usage() unless defined &$cmd;

my $res = PerlBench::Results->new($resdir);
die "No results found in $resdir.\n" unless $res;

{
    no strict 'refs';
    &$cmd(@ARGV);
}

sub cmd_list_hosts {
    for my $h ($res->hosts) {
	print "$h\n";
    }
}

sub _filter_perl {
    my $perl = shift;
    return 0 if $filter_activeperl && !($perl->{name} =~ /ActivePerl/);
    return 0 if $filter_gcc && !$perl->{config}{gccversion};
    if (@filter_version) {
	my $ok;
	for my $v (@filter_version) {
	    if ($perl->{version} =~ /^\Q$v\E(\.|$)/) {
		$ok = 1;
		last;
	    }
	}
	return 0 unless $ok;
    }
    return 1;
}

sub cmd_list_results {
    my @hosts = @_;
    my @perls = grep _filter_perl($_), $res->perls(@hosts);
    for my $p (@perls) {
	print "$p->{name}\n";
	print "    \@$p->{host}\n" unless @hosts == 1;
    CONFIG:
	for my $ck (sort keys %{$p->{config} || {}}) {
	    my $v = $p->{config}{$ck};
	    for my $p2 (@perls) {
		if ($v ne $p2->{config}{$ck}) {
		    $v = remove_common_words($v, map $_->{config}{$ck}, @perls)
			if $ck eq "ccflags" || $ck eq "optimize";
		    print "    # $ck = $v\n";
		    next CONFIG;
		}
	    }
	}
	my @t = @{$p->{t}};
	if ($merge) {
	    my %t;
	    for (@t) {
		push(@{$t{$_->{test}}}, $_);
	    }
	    @t = ();
	    for (values %t) {
		if (@$_ > 1) {
		    my %res = %{$_->[0]};
		    for my $f (qw(min med max avg)) {
			$res{$f} = calc_stats([map $_->{$f}, @$_])->{$f};
		    }
		    delete $res{loop_overhead};
		    delete $res{std_dev};  # XXX can it be merged?
		    push(@t, \%res);
		}
		else {
		    die unless @$_;
		    push(@t, $_->[0]);
		}
	    }
	}
	for my $t (sort {$b->{med} <=> $a->{med}} @t) {
	    my($name) = split(' ', $t->{test});
	    $name =~ s,^benchmarks/,,;
	    $name =~ s,\.b$,,;
	    printf "    %-35s %s\n", $name, sec_f($t->{med}, $t->{med} - $t->{min});
	}
    }
}

sub remove_common_words {
    my $w = $_[0];
    my %w;
    for (@_) {
	my %w2;
	for (split(' ')) {
	    $w2{$_}++;
	}
	for (keys %w2) {
	    $w{$_}++;
	}
    }
    return join(" ", grep $w{$_} != @_, split(' ', $w));
}

sub usage {
    (my $progname = $0) =~ s,.*/,,;
    die "Usage: $progname [options] <cmd> [cmd-options]\n";
}
