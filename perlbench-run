#!/usr/bin/perl -w

# $Id$

require 5.002;
use strict;
$| = 1;

my $VERBOSE;

package Perl;

sub new
{
    my($class, $path, $version) = @_;
    bless { path => $path, version => $version }, $class;
}

my $ld_path = Cwd::extLibpath()	 if $^O eq 'os2';
$ld_path .= ';'			 if $ld_path and $^O eq 'os2';

sub cmd
{
    my $self = shift;
    my $path = $self->{path};
    (my $pdir = $path) =~ s,[/\\][^/\\]+$,/,;
    if (-d "$pdir/lib") {
        # uninstalled perl
        Cwd::extLibpath_set("$ld_path$pdir") if $^O eq 'os2'; # Find DLL
	($path, '-I', "$pdir/lib");
    } else {
	$path;
    }
}

sub run_cmd
{
    my $self = shift;
    my @cmd = $self->cmd;
    my $fh = shift;
    my @args = map {/\s/ ? "'$_'" : $_} @_;
    print "Running '@cmd @args'...\n" if $VERBOSE;
    open($fh, "@cmd @args |") or die "Cannot pipe from '@cmd @args': $!";
}

package main;

use Getopt::Std;
use vars qw($opt_v $opt_c $opt_t $opt_s);
getopts("vc:t:s") or usage();

$VERBOSE = $opt_v;

my @perls;
for (@ARGV) {
   unless (-x $_) {
	warn "$_ is not executable, skipping.\n";
	next;
   }
   my $tmp = Perl->new($_, 'unknown');
   $tmp->run_cmd(*V, '-e', 'print qq(This is perl ), $]+0, qq(\n)');
   my $version = <V>;
   close V or die "closing pipe from perl: exit code $?";
   chomp $version;
   unless ($version =~ /^This is perl (\d+.\d+)/) {
	warn "$_ does not appear to be a working perl, skipping.\n";
	next;
   }
   push(@perls, Perl->new($_, $1+0));
}

usage() unless @perls;

# Show perl configurations
my $no = "A";
my $p;
for $p (@perls)
{
    print "\n" unless $no eq "A";
    print "$no) perl-$p->{version}\n";
    printf "\t%-11s = %s\n", "path", $p->{path};
    if ($p->{version} >= 5) {
	# The perl should have Configure support.  Try to extract
	# compiler info.
	my $prog = 'use Config; Config::config_vars(qw(cc optimize ccflags usemymalloc))';
	$p->run_cmd(*CONFIG, '-e', $prog);
	while (<CONFIG>) {
	   next unless /^(\w+)='([^']+)'/;  #' #
           $p->{$1} = $2;
	   printf "\t%-11s = %s\n", $1, $2;
        }
    }
    $no++;
}

my $factor = $opt_c;
unless ($factor) {
    $factor = `$^X cpu_factor`;
    chomp($factor);
    die "Can't calculate cpu speed factor" unless $factor;
}

print "\nCPU SPEED FACTOR = $factor\n" if $VERBOSE;

# Try to run tests
die "No test directory found" unless -d 't';

my @tests;

use File::Find;
find(sub { /\.t$/ && push(@tests, $File::Find::name) }, "t");
if ($opt_t) {
    @tests = grep /$opt_t/o, @tests;
}
@tests = sort @tests;

# Try to run the empty test in order to time the loop
for $p (@perls) {
    print "Timing empty loop for perl-$p->{version}\n" if $VERBOSE;
    $p->run_cmd(*P, "empty.t", $factor);
    while (<P>) {
        next unless /^CYCLES\/SEC:\s*(\S+)/;
        $p->{empty_cycles} = int($1);
	print if $VERBOSE;
    }
    close(P);
    die "Could not determine empty test speed for $p->{path}"
        unless  $p->{empty_cycles};
    $p->{point_sum} = 0;
}

unless ($VERBOSE) {
    $no = "A";
    print "\n";
    print " " x 20;
    for $p (@perls) {
	printf "%8s", $no;
	$no++;
    }
    print "\n";

    print " " x 20;
    for $p (@perls) {
	printf "%8s", "----";
    }
    print "\n";
}

my $test;
for $test (@tests) {
    unless (open(T, $test)) {
	warn "Can't open $test: $!";
	next;
    }
    my %prop;
    my $lines = 20;
    while (<T>) {
	last unless $lines--;
	next unless /^\#\s*(\w+)\s*:\s*(.*)/;
	my($k,$v) = (lc($1), $2);
	
	if (defined $prop{$k}) {
	    $prop{$k} .= "\n$v";
	} else {
	    $prop{$k} = $v;
	}
    }
    close(T);

    if ($VERBOSE) {
        print "Test properties:\n";
	for (keys %prop) {
	    print "\t$_ = $prop{$_}\n";
	}
    }

    my $name = $test;
    $name =~ s,^t/,,;
    $name =~ s,\.t$,,;
    printf "%-20s", $name unless $VERBOSE;

    my $scale;
    my $p;
    for $p (@perls) {
	if ($p->{version} < $prop{'require'}) {
	    # Can't run test
	    if ($VERBOSE) {
		print "Perl-$p->{version} can't run the test\n";
	    } else {
		printf "%8s", "N/A";
	    }
	    next;
	}
	my $points = 0;
	$p->run_cmd(*P, $test, $factor, $p->{empty_cycles});
	while (<P>) {
	    print ">>> $_" if $VERBOSE;
	    if (/^BENCH POINTS:\s+(\S+)/) {
		$points = $1;
	    }
	}
	close(P);

	unless ($VERBOSE) {
	    # present results
	    unless ($opt_s) {
		unless (defined $scale) {
		    $scale = 100 / $points;
		}
		$points *= $scale;
	    }
	    printf "%8.0f", $points;
	    $p->{point_sum} += $points;
	    $p->{no_tests}++;
	}
    }
    print "\n" unless $VERBOSE;
}

unless ($VERBOSE) {
    print "\n";
    printf "%-20s", "AVERAGE";
    for $p (@perls) {
        printf "%8.0f", $p->{point_sum} / $p->{no_tests};
    }
    print "\n";
}


sub usage
{
    $0 =~ s,.*/,,;
    die "Usage: $0 [options] <perl1> <perl2>...\n";
}

